/////////////////////////////////////////////////////////////////////
//
// Модуль часто используемых функций работы с файлами
//
// (с) EvilBeaver, 2016
//
/////////////////////////////////////////////////////////////////////


// Проверяет существование файла или каталога
//
// Параметры:
//   Путь - Проверяемый путь
//
//  Возвращаемое значение:
//   Булево   - Истина, если файл/каталог существует
//
Функция Существует(Знач Путь) Экспорт
    
    Объект = Новый Файл(Путь);
    
    Возврат Объект.Существует();
    
КонецФункции // Существует()

// Проверяет существование файла
//
// Параметры:
//   Путь - Проверяемый путь
//
//  Возвращаемое значение:
//   Булево   - Истина, если файл существует
//
Функция ФайлСуществует(Знач Путь) Экспорт
    
    Объект = Новый Файл(Путь);
    
    Возврат Объект.Существует() и Объект.ЭтоФайл();
    
КонецФункции // ФайлСуществует()

// Проверяет существование каталога
//
// Параметры:
//   Путь - Проверяемый путь
//
//  Возвращаемое значение:
//   Булево   - Истина, если каталог существует
//
Функция КаталогСуществует(Знач Путь) Экспорт
    
    Объект = Новый Файл(Путь);
    
    Возврат Объект.Существует() и Объект.ЭтоКаталог();
    
КонецФункции // КаталогСуществует()

// Гарантирует наличие пустого каталога с указанным именем
//
Процедура ОбеспечитьКаталог(Знач Путь)
    
    Объект = Новый Файл(Путь);
    Если Не Объект.Существует() Тогда
        СоздатьКаталог(Путь);
    ИначеЕсли Объект.ЭтоКаталог() Тогда
        УдалитьФайлы(Путь, ПолучитьМаскуВсеФайлы());
    Иначе
        ВызватьИсключение "Не удается создать каталог " + Путь;
    КонецЕсли;
    
КонецПроцедуры // ОбеспечитьКаталог()